buildscript {
    ext.kotlinVersion = '1.1.4'
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.1'
        classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:' + kotlinVersion
    }
}

allprojects {
    repositories {
        jcenter()
    }
}

subprojects {
    afterEvaluate {
        if (it.hasProperty('test')) {
            test {
                testLogging {
                    events "passed", "skipped", "failed"
                    exceptionFormat = 'full'
                }
                afterSuite { desc, result ->
                    if (!desc.parent) {
                        def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
                        println("\n$output\n")
                    }
                }
            }
        }
    }
}

def versions = [
        jackson             : '2.8.9',
        okhttp              : '3.8.1',
        retrofit            : '2.3.0'
]

ext.libraries = [
        kotlin              : 'org.jetbrains.kotlin:kotlin-stdlib-jre8:' + kotlinVersion,
        kotlinReflect       : 'org.jetbrains.kotlin:kotlin-reflect:' + kotlinVersion,

        jsoup               : 'org.jsoup:jsoup:1.10.3',
        jackson             : 'com.fasterxml.jackson.core:jackson-databind:' + versions.jackson,
        jacksonXml          : 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml:' + versions.jackson,
        jacksonYaml         : 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:' + versions.jackson,
        jacksonKotlin       : 'com.fasterxml.jackson.module:jackson-module-kotlin:' + versions.jackson,

        log4j2              : 'org.apache.logging.log4j:log4j-core:2.8.2',

        okhttp              : 'com.squareup.okhttp3:okhttp:' + versions.okhttp,
        okhttpLogging       : 'com.squareup.okhttp3:logging-interceptor:' + versions.okhttp,
        retrofit            : 'com.squareup.retrofit2:retrofit:' + versions.retrofit,
        retrofitJackson     : 'com.squareup.retrofit2:converter-jackson:' + versions.retrofit,

        postgres            : 'org.postgresql:postgresql:42.1.3',

        // test dependencies
        assertj             : 'org.assertj:assertj-core:3.8.0',
        mockwebserver       : 'com.squareup.okhttp3:mockwebserver:' + versions.okhttp
]
